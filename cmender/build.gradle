import java.text.SimpleDateFormat

plugins {
    id "java"
    id "distribution"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "commons-cli:commons-cli:1.8.0"
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    testImplementation platform("org.junit:junit-bom:5.10.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

sourceSets {
    main {
        java {
            srcDir "src"
        }

        resources {
            srcDir "resources"
        }
    }

    test {
        java {
            srcDir "test"
        }

        resources {
            srcDir "resources"
        }
    }
}

jar {
    archiveFileName.set("${jarName}.jar")
    manifest {
        attributes (
                "Main-Class": mainClass,
                "Created-By": "Gradle ${gradle.gradleVersion}, JDK ${JavaVersion.current().toString()} ${System.getProperty('java.vendor')}",
                "Implementation-Title": project.name,
                "X-Implementation-Description": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor,
                "X-Group": project.group,
                "X-Release-Date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
