import java.text.SimpleDateFormat

plugins {
    id "java"
    id "distribution"
    id "com.github.johnrengelman.shadow" version "8.1.0" // TODO Replace with the latest version
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

test.jvmArgs System.getProperty("test.jvmArgs")? System.getProperty("test.jvmArgs").split(" ") : ""

repositories {
    mavenCentral()
}

dependencies {
    implementation "commons-cli:commons-cli:1.8.0"
    implementation "org.apache.logging.log4j:log4j-core:2.23.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0"
    implementation "org.fusesource.jansi:jansi:2.4.0"
    implementation "commons-io:commons-io:2.11.0"
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"

    testCompileOnly "org.projectlombok:lombok:1.18.34"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.34"
    testImplementation platform("org.junit:junit-bom:5.10.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    testImplementation "org.hamcrest:java-hamcrest:2.0.0.0"
}

jar {
    archiveFileName.set("${jarName}.jar")
    manifest {
        attributes (
                "Main-Class": mainClass,
                "Multi-Release": true,
                "Created-By": "Gradle ${gradle.gradleVersion}, JDK ${JavaVersion.current().toString()} ${System.getProperty('java.vendor')}",
                "Implementation-Title": project.name,
                "X-Implementation-Description": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor,
                "X-Group": project.group,
                "X-Release-Date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
        )
    }
}

shadowJar {
    archiveBaseName.set("${jarName}")
    archiveClassifier.set('')
    archiveVersion.set('')
}

javadoc {
    // Set the title of the generated Javadoc
    options {
        title = "CMender Documentation"
        options.addStringOption('Xdoclint:all', '-quiet')
        options.addStringOption('charset', 'UTF-8')
    }

    // Specify source directories if you have multiple source sets
    // source = sourceSets.main.allJava

    // Configure the Javadoc task to include specific packages or classes
    exclude '**/internal/**' // Exclude internal packages
}
